apply plugin: 'com.android.application'
import org.apache.tools.ant.taskdefs.condition.Os

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.samsung.vd.cinemacontrolpanel"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        buildConfigField "long", "BUILD_DATE", System.currentTimeMillis() + "L"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //
    // For Android JNI
    //
    sourceSets.main {
        jniLibs.srcDir 'src/main/libs'
        jni.srcDirs = []
    }

    //noinspection GroovyAssignabilityCheck
    task ndkBuild(type: Exec, description:'Compile JNI source via NDK') {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

        def command = properties.getProperty('ndk.dir')
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            command += "\\ndk-build.cmd"
        } else {
            command += "/ndk-build"
        }
        commandLine command, '-C', file('src/main').absolutePath
    }
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile project(':BaseUtils')
}
